import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
}

group 'org.citygml4j.tools'
version '2.1.0-SNAPSHOT'
description 'Collection of tools for processing CityGML files'

ext {
    date = new Date()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

dependencies {
    implementation 'org.citygml4j:citygml4j-xml:3.0.0-SNAPSHOT'
    implementation 'org.citygml4j:citygml4j-cityjson:3.0.0-SNAPSHOT'
    implementation 'info.picocli:picocli:4.7.1'
    implementation 'org.geotools:gt-epsg-extension:29.0'
    implementation 'org.geotools:gt-epsg-hsql:29.0'
    implementation 'org.geotools:gt-referencing:29.0'
    implementation 'org.apache.commons:commons-imaging:1.0-alpha3'
    runtimeOnly 'com.fasterxml.woodstox:woodstox-core:6.5.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

processResources {
    outputs.upToDateWhen { false }
    filteringCharset = 'ISO-8859-1'
    filesMatching('**/application.properties') {
        filter(ReplaceTokens, tokens: [
                name: project.name,
                version: project.version
        ])
    }
}

application {
    mainClass = 'org.citygml4j.tools.CityGMLTools'
    applicationDefaultJvmArgs = ['-Xms1G']
    executableDir = ''

    startScripts {
        doLast {
            def windowsScript = getWindowsScript()
            def unixScript = getUnixScript()
            windowsScript.text = windowsScript.text.replaceFirst('set APP_HOME=.*', '$0\r\nset WORKING_DIR=%CD%')
            unixScript.text = unixScript.text.replaceFirst('APP_HOME=.*', '$0\nexport APP_HOME\nexport WORKING_DIR=\\$SAVED')
        }
    }
}

distributions.main.contents {
    from('resources/README') {
        filter(ReplaceTokens, tokens: [
                name: project.name,
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: java.sourceCompatibility.toString()
        ])
    }
    into('license') {
        from 'LICENSE'
    }
    into('lib') {
        from('resources/logging.properties')
    }
    from(file("$buildDir/tmp/dir")) {
        mkdir "$buildDir/tmp/dir/extensions"
    }
}